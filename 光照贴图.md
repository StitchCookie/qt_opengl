## 漫反射贴图:

- 顶点着色器

  

```
#version 330 core
layout(location = 0) in vec3 aPos;
layout(location = 1) in vec3 aNormal; // 法向量
layout(location = 2) in vec2 aTexCoords;
out vec2 TexCoords;
uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;
out vec3 Normal;
out vec3 FragPos; // 顶点的世界坐标位置

void main(){
     gl_Position =  projection * view * model * vec4(aPos, 1.0);
     FragPos = vec3(model * vec4(aPos,1.0f));
     // inverse 求逆  transpose求转置
     //在漫反射光照部分，光照表现并没有问题，这是因为我们没有对物体进行任何缩放操作，所以我们并不真的需要使用一个法线矩阵，而是仅以模型矩阵乘以法线就可以。
     // 但是如果你会进行不等比缩放，使用法线矩阵去乘以法向量就是必须的了。 Normal = mat3(model) * aNormal;
      Normal = mat3(transpose(inverse(model))) * aNormal;
      TexCoords = aTexCoords;
}
```

- 片段着色器

  ```
  #version 330 core
  struct Material {
      sampler2D diffuse;  // 漫反射贴图
      vec3 specular;
      float shininess;
  };
  
  struct Light {
    vec3 positon;
  
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
  };
  
  out vec4 FragColor;
  in vec3 Normal;
  in vec3 FragPos;
  
  uniform Material material;
  uniform Light light;
  
  uniform vec3 viewPos;  // 摄像机观察位置
  
  in vec2 TexCoords;  // 纹理坐标
  
  void main(){
  
      // 设置环境光 = 光源Xambient材质属性
      vec3 ambient = light.ambient * texture(material.diffuse, TexCoords).rgb;
  
      // 将法线单位化
      vec3 norm = normalize(Normal);
      // 将光的方向向量单位化
      vec3 lightDir = normalize(light.positon -  FragPos);
      // 只有在小于90度时才会贡献漫反射光照
      float diff = max(dot(norm,lightDir),0.0f);
      // 漫反射光照值 = 光源 X 角度 X diffuse材质向量
      vec3 diffuse= light.diffuse * diff * texture(material.diffuse, TexCoords).rgb;
  
      // 镜面光
      vec3 viewDir = normalize(viewPos - FragPos);   // 视角方向向量  摄像机位置向量减去顶点向量
      vec3 reflectDir = reflect(-lightDir,norm);  // 光源反射的方向向量
      // 计算镜面分量 最后，这个部分将余弦值的32次方，这通常用来增强高光部分，使其更加明显。
      // 这样做是为了增强镜面高光的效果，使得夹角越小的区域有更强的高光
      float spec = pow(max(dot(viewDir,reflectDir),0.0),material.shininess);
      // 散射属性 = 光源 * 镜面高光的散射/半径 * 散射属性
      vec3 specular = light.specular * (spec * material.specular);
      vec3 result = ambient + diffuse + specular;
      FragColor = vec4(result,1.0f);
  }
  ```

## 练习:

1. ### 调整光源的环境光、漫反射和镜面光向量，看看它们如何影响箱子的视觉输出。

- ​       对于环境光,也就是无论光源能不能照射到该表面,也会表现出来的颜色,并且被光源照不到的表面也只有环境光亮有作用.
- 对于漫反射,也就是根据光源照射到物体表面的角度来均匀的发散出来的，会有一直层次光感，不会向环境光一样。漫反射是越接近光源的地方光照效果越明显
- 对于镜面光向量,其实就是真正意义上反射回来的光线,但由于人视角可能与反射的光线存在夹角。镜面反射是越靠近入射光反射的方向越明显。

2  尝试在片段着色器中反转镜面光贴图的颜色值，让木头显示镜面高光而钢制边缘不反光（由于钢制边缘中有一些裂缝，边缘仍会显示一些镜面高光，虽然强度会小很多）

```c++
// 修改片段着色器
vec3 specular = light.specular * (spec * (vec3(1.0) - texture(material.specular,TexCoords).rgb));
```

3 使用漫反射贴图创建一个彩色而不是黑白的镜面光贴图，看看结果看起来并不是那么真实了

![image-20240314134416933](C:\Users\18071\AppData\Roaming\Typora\typora-user-images\image-20240314134416933.png)

4 添加一个叫做放射光贴图(Emission Map)的东西，它是一个储存了每个片段的发光值(Emission Value)的贴图。发光值是一个包含（假设）光源的物体发光(Emit)时可能显现的颜色，这样的话物体就能够忽略光照条件进行发光(Glow)。游戏中某个物体在发光的时候，你通常看到的就是放射光贴图（比如 [机器人的眼](https://learnopengl-cn.github.io/img/02/04/shaders_enemy.jpg)，或是[箱子上的灯带](https://learnopengl-cn.github.io/img/02/04/emissive.png)）。将[这个](https://learnopengl-cn.github.io/img/02/04/matrix.jpg)纹理（作者为 creativesam）作为放射光贴图添加到箱子上，产生这些字母都在发光的效果

![image-20240314154816663](C:\Users\18071\AppData\Roaming\Typora\typora-user-images\image-20240314154816663.png)